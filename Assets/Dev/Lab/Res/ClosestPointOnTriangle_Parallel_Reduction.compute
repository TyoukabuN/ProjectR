#pragma kernel CSMain

RWStructuredBuffer<float3> vertexBuf;
RWStructuredBuffer<int> indexBuf;
RWStructuredBuffer<float3> closestPointBuf;
RWStructuredBuffer<float4> debugBuf;

int taskAmount;
float4 checkPoint;
float4x4 local2world;

groupshared float3 Shared_hitPoint2triPoint[1024];

float3 ClosestPointOnTriangle(float3 p, float3 a, float3 b, float3 c);

//TODO:这里的线程组固定为1024，后面可能需要增加512，256来适配小面数的模型
[numthreads(1024,1,1)]
void CSMain(uint3 groupThreadID : SV_DispatchThreadID, uint localIndex : SV_GroupIndex, uint3 groupID : SV_GroupID)
{
    uint id = groupThreadID.x;
    
    debugBuf[groupThreadID.x].x = id;

    //在使用这个shader的时候会传入vertices和indices
    //直接在shader里获取Mesh里的每个triangle
    //所以实际有效执行的任务数会是taskAmound =indices/3
    //所以当id>=taskAmount时是拿不到对应的index的,也就是说拿不多对应(正确)的triangle的
    //所以这个给taskAmount的结果直接填一个极远的向量,确保这个结果(点)无效,在归约的时候会被过滤掉
    if (id >= taskAmount)
    {
        Shared_hitPoint2triPoint[localIndex] = float3(999999, 999999, 999999);
    }
    else
    {
        int sIndex = id * 3;
        float3 p1 = mul(local2world, float4(vertexBuf[indexBuf[sIndex + 0]],1)).xyz;
        float3 p2 = mul(local2world, float4(vertexBuf[indexBuf[sIndex + 1]],1)).xyz;
        float3 p3 = mul(local2world, float4(vertexBuf[indexBuf[sIndex + 2]],1)).xyz;

        debugBuf[groupThreadID.x].y = sIndex;
        debugBuf[groupThreadID.x].z = 0;
        debugBuf[groupThreadID.x].w = groupID.x;
    
        float3 triPoint = ClosestPointOnTriangle(checkPoint.xyz, p1, p2, p3);
    
        float dis2hitPoint = distance(triPoint, checkPoint.xyz);
        float3 hitPoint2triPoint = triPoint - checkPoint.xyz;
    
        Shared_hitPoint2triPoint[localIndex] = hitPoint2triPoint;
    }
    
    GroupMemoryBarrierWithGroupSync();
    
    //并行归约
    for (uint stride = 512; stride > 0; stride >>= 1)
    {
        if (localIndex < stride)
        {
            if (length(Shared_hitPoint2triPoint[localIndex + stride]) < length(Shared_hitPoint2triPoint[localIndex]))
                Shared_hitPoint2triPoint[localIndex] = Shared_hitPoint2triPoint[localIndex + stride];
        }
        GroupMemoryBarrierWithGroupSync();
    }
    
    if (localIndex == 0)
    {
        closestPointBuf[groupID.x] = Shared_hitPoint2triPoint[0] + checkPoint.xyz;
    }
}

//Gen by AI
//找点到三角面上的最近点
float3 ClosestPointOnTriangle(float3 p, float3 a, float3 b, float3 c)
{
    // Compute edges and vectors
    float3 ab = b - a;
    float3 ac = c - a;
    float3 ap = p - a;

    // Compute dot products
    float d1 = dot(ab, ap);
    float d2 = dot(ac, ap);

    // Check if P in vertex region outside A
    if (d1 <= 0.0 && d2 <= 0.0)
        return a;

    // Compute vectors and dot products for B
    float3 bp = p - b;
    float d3 = dot(ab, bp);
    float d4 = dot(ac, bp);

    // Check if P in vertex region outside B
    if (d3 >= 0.0 && d4 <= d3)
        return b;

    // Check if P in edge region of AB, if so return projection
    float vc = d1 * d4 - d3 * d2;
    if (vc <= 0.0 && d1 >= 0.0 && d3 <= 0.0)
    {
        float v = d1 / (d1 - d3);
        return a + v * ab;
    }

    // Compute vectors and dot products for C
    float3 cp = p - c;
    float d5 = dot(ab, cp);
    float d6 = dot(ac, cp);

    // Check if P in vertex region outside C
    if (d6 >= 0.0 && d5 <= d6)
        return c;

    // Check if P in edge region of AC, if so return projection
    float vb = d5 * d2 - d1 * d6;
    if (vb <= 0.0 && d2 >= 0.0 && d6 <= 0.0)
    {
        float w = d2 / (d2 - d6);
        return a + w * ac;
    }

    // Check if P in edge region of BC, if so return projection
    float va = d3 * d6 - d5 * d4;
    if (va <= 0.0 && (d4 - d3) >= 0.0 && (d5 - d6) >= 0.0)
    {
        float u = (d4 - d3) / ((d4 - d3) + (d5 - d6));
        return b + u * (c - b);
    }

    // P inside face region. Compute barycentric coordinates (u, v, w)
    float denom = 1.0 / (va + vb + vc);
    float v = vb * denom;
    float w = vc * denom;
    return a + ab * v + ac * w;
}
